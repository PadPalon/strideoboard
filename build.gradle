group "strideoboard"
version "0.1-alpha1"

apply plugin: "java"
apply plugin: "scala"
apply plugin: "gradle-one-jar"

sourceCompatibility = 1.8
def bundleFolder = "build/bundle"

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.github.rholder:gradle-one-jar:1.0.4"
    }
}

dependencies {
    compile group: "org.scala-lang", name: "scala-library", version: "2.12.0"

    compile group: "uk.co.caprica", name: "vlcj", version: "3.0.1"
    compile group: "com.github.tulskiy", name: "jkeymaster", version: "1.2"

    compile group: "com.google.guava", name: "guava", version: "20.0"
    compile "com.google.code.findbugs:jsr305:3.0.1"

    compile "org.testng:testng:6.9.10"
}

sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []

jar {
    manifest {
        attributes "Main-Class": "ch.neukom.strideoboard.Strideoboard"
    }
}

task clearDistributions(type: Delete) {
    delete "${bundleFolder}"
    delete "build/distributions"
}

task fatJar(type: OneJar) {
    mainClass = "ch.neukom.strideoboard.Strideoboard"
    archiveName = "${rootProject.name}.jar"
}

task copyJar(type: Copy) {
    from "build/libs/${rootProject.name}.jar"
    into "${bundleFolder}"
}

task copyConfigs(type: Copy) {
    from "src/main/resources"
    into "${bundleFolder}/config"
}

task copyTask

task createZip(type: Zip) {
    from "${bundleFolder}"
}

compileJava.dependsOn clearDistributions
copyJar.dependsOn fatJar
copyTask.dependsOn copyConfigs, copyJar
createZip.dependsOn build, copyTask